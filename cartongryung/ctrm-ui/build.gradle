buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1'

java {
    sourceCompatibility = '17'
}

configurations {
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    runtimeOnly 'com.mysql:mysql-connector-j:8.0.33'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'

    // QueryDSL 라이브러리
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // springSecurity
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'commons-io:commons-io:2.11.0'

    // 썸네일
    implementation 'net.coobird:thumbnailator:0.4.1'

    // 외부 api
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // jwt 토큰
    implementation 'com.nimbusds:nimbus-jose-jwt:9.37.2'

    // 이메일
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // Bouncy Castle 라이브러리 추가
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'

    // OkHttp 라이브러리 추가
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    implementation 'org.json:json:20210307'

    implementation 'com.google.firebase:firebase-admin:9.2.0'

    // Swagger configuration
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    runtimeOnly 'com.github.therapi:therapi-runtime-javadoc:0.15.0'
    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
}

// Querydsl 설정부
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}

sourceSets {
    main.resources.excludes = [ 'static/**/*.html', 'static/images/**' ]
}


tasks.named('test') {
    useJUnitPlatform()
}

jar {
    enabled = false
}

javadoc {
    options {
        encoding = 'UTF-8'          // Java 소스 파일의 인코딩
        charSet = 'UTF-8'           // 생성된 HTML 파일의 문자 인코딩
        docEncoding = 'UTF-8'       // JavaDoc의 출력 인코딩
        locale = 'ko_KR'            // JavaDoc 생성 시 사용할 로케일 설정
    }
}
